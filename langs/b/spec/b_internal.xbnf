<b> ::= declarations=<declaration>+;

<declaration> ::=

  "fn" name=<function> "\(" params=<flattened_parameter_list>? "\)" body=<block>;

<block> ::=

  statement=<statement> |

  "{" statements=<statement>* "}";

<statement> ::=

  lhs=(
    <variable> |
    <array_access>
  ) "=" rhs=(
    <operand> |
    <expression> |
    fn="alloc" "\(" arg=<operand> "\)" |
    fn="free" "\(" arg=<operand> "\)" |
    fn="stoi" "\(" arg=<operand> "\)" |
    fn=<function> "\(" args=<flattened_argument_list>? "\)"
  ) ";" |

  fn=("print" | "printi" | "read") "\(" arg=<operand> "\)" ";" |

  "while" "\(" cond=<expression> "\)" body=<block> |

  "if" "\(" cond=<expression> "\)" body=<block> |

  "return" ret=<operand>? ";" |

  fn=<function> "\(" args=<flattened_argument_list>? "\)" ";";

<flattened_parameter_list> ::= <variable>*;

<flattened_argument_list> ::= <variable>*;

<array_access> ::= arr=<variable> "\[" idx=<operand> "\]";

<expression> ::=

  uop=<unary_operator> op=<operand> |

  lop=<operand> bop=<binary_operator> rop=<operand>;

<operand> ::= <variable> | <string> | <array_access> | decimal_integer;

<unary_operator> ::= "!";

<binary_operator> ::=

  "\+" | "-"  | "\*" | "/"  |

  "%"  | "\|" | "&"  | "\^" |

  "==" | "!=" | ">"  | ">=" |

  "<"  | "<=" | "<<" | ">>";

alias <function> ::= identifier;

alias <variable> ::= identifier;

alias <string> ::= escaped_string;
