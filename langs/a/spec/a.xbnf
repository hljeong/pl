<a> ::= sections=<section>*;

<section> ::=  <code_section> | <data_section>;

<data_section> ::= "[data]" definitions=<constant_definition>*;

<constant_definition> ::= label=<label> ":" string=escaped_string;

<code_section> ::= "[code]" instructions=<labeled_instruction>*;

<labeled_instruction> ::= labels=(label=<label> ":")* ins=<instruction>;

<instruction> ::= ins=<legal_instruction> | ins=<pseudoinstruction>;

<legal_instruction> ::=

  <b_type_instruction> |

  <oi_type_instruction> |

  <m_type_instruction> |

  <o_type_instruction> |

  <j_type_instruction> |

  <e_type_instruction>;

<b_type_instruction> ::= cmd=<b_type_command> src1=<reg> src2=<reg> off=<imm>;

<oi_type_instruction> ::= cmd=<oi_type_command> dst=<reg> src=<reg> imm=<imm>;

<m_type_instruction> ::= cmd=<m_type_command> reg=<reg> base=<reg> off=<imm>;

<o_type_instruction> ::= cmd=<o_type_command> dst=<reg> src1=<reg> src2=<reg>;

<j_type_instruction> ::= cmd=<j_type_command> off=<imm>;

<e_type_instruction> ::= cmd=<e_type_command>;

<b_type_command> ::= "beq" | "bne";

<oi_type_command> ::=

  "addi" | "subi" | "muli" | "divi" |

  "modi" | "ori"  | "andi" | "xori" |

  "eqi"  | "gti"  | "gei"  | "lti"  |

  "lei"  | "lsi"  | "rsi";

<m_type_command> ::= "l" | "s";

<o_type_command> ::=

  "add" | "sub" | "mul" | "div" |

  "mod" | "or"  | "and" | "xor" |

  "eq"  | "gt"  | "ge"  | "lt"  |

  "le"  | "ls"  | "rs";

<j_type_command> ::= "j";

<e_type_command> ::= "e" | "eb";

<pseudoinstruction> ::=

  "jr" addr=<reg> |

  "j" off=<lbl> |

  "b" cond=<reg> off=<imm> |

  "b" cond=<reg> off=<lbl> |

  "beq" src1=<reg> src2=<reg> off=<lbl> |

  "bne" src1=<reg> src2=<reg> off=<lbl> |

  "ne" dst=<reg> src1=<reg> src2=<reg> |

  "nei" dst=<reg> src=<reg> imm=<imm> |

  "not" dst=<reg> src=<reg> |

  "set" dst=<reg> src=<reg> |

  "setv" dst=<reg> val=<val> |

  "setv" dst=<reg> val=<lbl> |

  cmd="addv" dst=<reg> src=<reg> val=<val> |

  cmd="subv" dst=<reg> src=<reg> val=<val> |

  cmd="mulv" dst=<reg> src=<reg> val=<val> |

  cmd="divv" dst=<reg> src=<reg> val=<val> |

  cmd="modv" dst=<reg> src=<reg> val=<val> |

  cmd="orv" dst=<reg> src=<reg> val=<val> |

  cmd="andv" dst=<reg> src=<reg> val=<val> |

  cmd="xorv" dst=<reg> src=<reg> val=<val> |

  cmd="eqv" dst=<reg> src=<reg> val=<val> |

  cmd="nev" dst=<reg> src=<reg> val=<val> |

  cmd="gtv" dst=<reg> src=<reg> val=<val> |

  cmd="gev" dst=<reg> src=<reg> val=<val> |

  cmd="ltv" dst=<reg> src=<reg> val=<val> |

  cmd="lev" dst=<reg> src=<reg> val=<val> |

  cmd="lsv" dst=<reg> src=<reg> val=<val> |

  cmd="rsv" dst=<reg> src=<reg> val=<val>;

alias <label> ::= identifier;

alias <reg> ::= identifier;

alias <imm> ::= decimal_integer;

alias <lbl> ::= identifier;

alias <val> ::= decimal_integer;
