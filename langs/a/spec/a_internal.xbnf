<a> ::= <code_section> <data_section>;

<data_section> ::= <constant_definition>*;

<constant_definition> ::= escaped_string;

<code_section> ::= <instruction>*;

<instruction> ::= <legal_instruction> | <pseudoinstruction>;

<legal_instruction> ::=

  <b_type_instruction> |

  <oi_type_instruction> |

  <m_type_instruction> |

  <o_type_instruction> |

  <j_type_instruction> |

  <e_type_instruction>;

<b_type_instruction> ::= <b_type_command> <reg> <reg> <imm>;

<oi_type_instruction> ::= <oi_type_command> <reg> <reg> <imm>;

<m_type_instruction> ::= <m_type_command> <reg> <reg> <imm>;

<o_type_instruction> ::= <o_type_command> <reg> <reg> <reg>;

<j_type_instruction> ::= <j_type_command> <imm>;

<e_type_instruction> ::= <e_type_command>;

<b_type_command> ::= "beq" | "bne";

<oi_type_command> ::=

  "addi" | "subi" | "muli" | "divi" |

  "modi" | "ori"  | "andi" | "xori" |

  "eqi"  | "gti"  | "gei"  | "lti"  |

  "lei"  | "lsi"  | "rsi";

<m_type_command> ::= "l" | "s";

<o_type_command> ::=

  "add" | "sub" | "mul" | "div" |

  "mod" | "or"  | "and" | "xor" |

  "eq"  | "gt"  | "ge"  | "lt"  |

  "le"  | "ls"  | "rs";

<j_type_command> ::= "j";

<e_type_command> ::= "e" | "eb";

<pseudoinstruction> ::=

  "jr" <reg> |

  "j" <lbl> |

  "b" <reg> <imm> |

  "ne" <reg> <reg> <reg> |

  "nei" <reg> <reg> <imm> |

  "not" <reg> <reg> |

  "set" <reg> <reg> |

  "setv" <reg> <val> |

  "setv" <reg> <lbl> |

  "addv" <reg> <reg> <val> |

  "subv" <reg> <reg> <val> |

  "mulv" <reg> <reg> <val> |

  "divv" <reg> <reg> <val> |

  "modv" <reg> <reg> <val> |

  "orv" <reg> <reg> <val> |

  "andv" <reg> <reg> <val> |

  "xorv" <reg> <reg> <val> |

  "eqv" <reg> <reg> <val> |

  "nev" <reg> <reg> <val> |

  "gtv" <reg> <reg> <val> |

  "gev" <reg> <reg> <val> |

  "ltv" <reg> <reg> <val> |

  "lev" <reg> <reg> <val> |

  "lsv" <reg> <reg> <val> |

  "rsv" <reg> <reg> <val>;

alias <reg> ::= identifier;

alias <imm> ::= decimal_integer;

alias <lbl> ::= identifier;

alias <val> ::= decimal_integer;
